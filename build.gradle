plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.61'
    id 'com.github.node-gradle.node' version '2.2.3'
}

group = 'com.github.sipe90'
version = '0.7.2'

java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions:1.0.2.RELEASE'

    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'org.dizitart:potassium-nitrite:3.4.2'
    implementation 'net.dv8tion:JDA:4.2.0_168'
    implementation 'club.minnced:jda-reactor:1.2.0'
    implementation 'com.sedmelluq:lavaplayer:1.3.53'
    implementation 'com.sedmelluq:jda-nas:1.1.0'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

node {
    version = '14.15.4'
    download = true
}

processResources.dependsOn 'npmBuild'

task npmBuild(type: NpmTask) {
    dependsOn npmInstall

    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    inputs.files fileTree('src/main/webapp')

    outputs.dir 'build/webapp'

    description = "Bundles production version of the webapp"
    args = ["run", "build"]
}

bootJar {
    from('build/webapp') {
        into 'static'
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '1.8'
    }
}
