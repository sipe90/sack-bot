plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.10'
    id 'com.github.node-gradle.node' version '3.1.1'
    id 'nebula.release' version '16.0.0'
}

group = 'com.github.sipe90'

java.sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'io.projectreactor.kotlin:reactor-kotlin-extensions'

    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    implementation 'ch.qos.logback:logback-classic:1.2.9'

    implementation 'org.dizitart:potassium-nitrite:3.4.3'
    implementation 'net.dv8tion:JDA:4.4.0_352'
    implementation 'com.github.MinnDevelopment:jda-reactor:1.4.0'
    implementation 'com.sedmelluq:lavaplayer:1.3.78'
    implementation 'com.sedmelluq:jda-nas:1.1.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

node {
    version = '16.14.2'
    download = true
}

processResources.dependsOn 'npmBuild'

task npmBuild(type: NpmTask) {
    dependsOn npmInstall

    args = ['run', 'build']
    description = 'Bundles production version of the webapp'

    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    inputs.files fileTree('src/main/webapp')

    outputs.dir 'build/webapp'
}

task npmBumpVersion(type: NpmTask) {
    args = ['run', 'bump-version', "${project.version}"]
    description = 'Updates front-end npm package version'
}

task npmReleaseVersion(type: NpmTask) {
    dependsOn tasks.npmBumpVersion
    args = ['run', 'commit-version']
    description = 'Commits the updated front-end package version to version control'
}

bootJar {
    from('build/webapp') {
        into 'static'
    }
}

bootBuildImage {
    imageName = "sipe90/${project.name}:${project.version}"
    publish = true
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '11'
    }
}

tasks.devSnapshotSetup.dependsOn tasks.npmBumpVersion
tasks.snapshotSetup.dependsOn tasks.npmBumpVersion
tasks.candidateSetup.dependsOn tasks.npmReleaseVersion
tasks.finalSetup.dependsOn tasks.npmReleaseVersion

tasks.release.dependsOn tasks.bootBuildImage
